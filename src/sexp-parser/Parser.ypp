/* -*- C++ -*- */
/*
 * Copyright 2016 WebAssembly Community Group participants
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

%skeleton "lalr1.cc"

/* Textual parser for filter s-expressions */

%defines
%define api.namespace {wasm::filt}
%define parser_class_name {Parser}

%define api.token.constructor
%define api.value.type variant
%define parse.assert

%code requires {
#include "sexp/Ast.h"
#include <string>

namespace wasm {
namespace filt {
class Driver;
class Node;
}}

#ifdef __clang__
// TODO: Limit the reach of these pragma's?
#pragma clang diagnostic ignored "-Wdeprecated-register"
#pragma clang diagnostic ignored "-Wundefined-inline"
#endif // __clang__
}

// The parsing context.
%param { class Driver& Driver }

%locations
%initial-action
{
  // Initialize the initial location.
  @$.begin.filename = @$.end.filename = &Driver.getFilename();
};

%define parse.trace
%define parse.error verbose

%code
{
// Note: Must follow definitions above, so that location_type is declared.
# include "Driver.h"
}

// Special tokens
%token END 0 "$END"

// Keywords
%token APPEND        "append"
%token AST_TO_BIT    "ast.to.bit"
%token AST_TO_BYTE   "ast.to.byte"
%token AST_TO_INT    "ast.to.int"
%token BIT_TO_BIT    "bit.to.bit"
%token BIT_TO_BYTE   "bit.to.byte"
%token BIT_TO_INT    "bit.to.int"
%token BIT_TO_AST    "bit.to.ast"
%token BYTE_TO_BIT   "byte.to.bit"
%token BYTE_TO_BYTE  "byte.to.byte"
%token BYTE_TO_INT   "byte.to.int"
%token BYTE_TO_AST   "byte.to.ast"
%token CALL          "call"
%token CASE          "case"
%token CLOSEPAREN    ")"
%token COPY          "copy"
%token DEFAULT       "default"
%token DEFINE        "define"
%token ERROR         "error"
%token EVAL          "eval"
%token EXTRACT       "extract"
%token EXTRACT_BEGIN "extract.begin"
%token EXTRACT_END   "extract.end"
%token EXTRACT_EOF   "extract.eof"
%token FILTER        "filter"
%token IF            "if"
%token INT_TO_BIT    "int.to.bit"
%token INT_TO_BYTE   "int.to.byte"
%token INT_TO_INT    "int.to.int"
%token INT_TO_AST    "int.to.ast"
%token I32_CONST     "i32.const"
%token I64_CONST     "i64.const"
%token LIT           "lit"
%token LOOP          "loop"
%token LOOP_UNBOUNDED "loop.unbounded"
%token OPENPAREN     "("
%token MAP           "map"
%token METHOD        "method"
%token PEEK          "peek"
%token POSTORDER     "postorder"
%token PREORDER      "preorder"
%token READ          "read"
%token SECTION       "section"
%token SELECT        "select"
%token SEQ           "seq"
%token SYM_CONST     "sym.const"
%token UINT8         "uint8"
%token UINT32        "uint32"
%token UINT64        "uint64"
%token U32_CONST     "u32.const"
%token U64_CONST     "u64.const"
%token VALUE         "value"
%token VARINT32      "varint32"
%token VARINT64      "varint64"
%token VARUINT1      "varuint1"
%token VARUINT7      "varuint7"
%token VARUINT32     "varuint32"
%token VARUINT64     "varuint64"
%token VERSION       "version"
%token VOID          "void"
%token WRITE         "write"

// Terminal classes
%token <std::string> IDENTIFIER
%token <std::string> QUOTED_IDENTIFIER
%token <uint64_t> INTEGER

// Nonterminal classes.
%type <wasm::filt::Node *> case
%type <wasm::filt::Node *> case_list
%type <wasm::filt::Node *> case_statement_list
%type <wasm::filt::Node *> declaration
%type <wasm::filt::Node *> declaration_list
%type <wasm::filt::Node *> expression
%type <wasm::filt::Node *> extract_statement_list
%type <wasm::filt::Node *> file
%type <wasm::filt::Node *> filter
%type <wasm::filt::Node *> filter_list
%type <wasm::filt::Node *> header
%type <wasm::filt::Node *> integer
%type <wasm::filt::Node *> loop_body
%type <wasm::filt::Node *> method
%type <wasm::filt::Node *> method_list
%type <wasm::filt::Node *> method_statement_list
%type <wasm::filt::Node *> name
%type <wasm::filt::Node *> section
%type <wasm::filt::Node *> section_list
%type <wasm::filt::Node *> statement
%type <wasm::filt::Node *> statement_list
%type <wasm::filt::Node *> symbol

%start file

%%

file    : section_list {
            $$ = $1;
            Driver.setParsedAst($1);
          }
        ;

case    : "(" "case" case_statement_list ")" { $$ = $3; }
        ;

case_list
        : expression statement { // selector / default statement.
            $$ =  Nary<NodeType::Select>::create();
            $$->append($1);
            $$->append($2);
          }
        | case_list case {
            $$ = $1;
            $$->append($2);
          }
        ;

case_statement_list
        : integer statement { // case index / first statement of case.
            $$ = Nary<NodeType::Case>::create();
            $$->append($1);
            $$->append($2);
          }
        | case_statement_list statement { // remaining statements of case.
            $$ = $1;
            $$->append($2);
          }
        ;

declaration
        : "(" method_list ")" { $$ = $2; }
        ;

declaration_list
        : symbol declaration {  // Section name / first define.
            $$ = Nary<NodeType::Section>::create();
            $$->append($1);
            $$->append($2);
          }
          | declaration_list declaration { // Additional defines.
            $$ = $1;
            $$->append($2);
          }
        ;

expression
        : "(" "append" ")" {
            $$ = Nullary<NodeType::AppendNoArgs>::create();
          }
        | "(" "append" expression ")" {
            $$ = Unary<NodeType::AppendOneArg>::create($3);
          }
        | "(" "call" name ")" {
            $$ = Unary<NodeType::Call>::create($3);
          }
        | "(" "error" ")" {
            $$ = Nullary<NodeType::Error>::create();
          }
        | "(" "eval" symbol ")" {
            $$ = Unary<NodeType::Eval>::create($3);
          }
        | "(" "extract.begin" integer ")" {
            $$ = Unary<NodeType::ExtractBegin>::create($3);
          }
        | "(" "extract.end" integer ")" {
            $$ = Unary<NodeType::ExtractEnd>::create($3);
          }
        | "(" "extract.eof" ")" {
            $$ = Nullary<NodeType::ExtractEof>::create();
          }
        | "(" "i32.const" integer ")" {
            $$ = Unary<NodeType::I32Const>::create($3);
          }
        | "(" "i64.const" integer ")" {
            $$ = Unary<NodeType::I64Const>::create($3);
          }
        | "(" "lit" integer ")" {
            $$ = Unary<NodeType::Lit>::create($3);
          }
        | "(" "map" expression expression ")" {
            $$ = Binary<NodeType::Map>::create($3, $4);
          }
        | "(" "peek" expression ")" {
            $$ = Unary<NodeType::Peek>::create($3);
          }
        | "(" "postorder" integer ")" {
            $$ = Unary<NodeType::Postorder>::create($3);
          }
        | "(" "preorder" integer ")" {
            $$ = Unary<NodeType::Preorder>::create($3);
          }
        | "(" "read" expression ")" {
            $$ = Unary<NodeType::Read>::create($3);
          }
        | "(" "sym.const" symbol ")" {
            $$ = Unary<NodeType::SymConst>::create($3);
          }
        | "(" "uint8" ")" {
            $$ = Nullary<NodeType::Uint8>::create();
          }
        | "(" "uint32" ")" {
            $$ = Nullary<NodeType::Uint32NoArgs>::create();
          }
        | "(" "uint32" integer ")"  {
            $$ = Unary<NodeType::Uint32OneArg>::create($3);
          }
        | "(" "uint64" ")" {
            $$ = Nullary<NodeType::Uint64NoArgs>::create();
          }
        | "(" "uint64" integer ")"  {
            $$ = Unary<NodeType::Uint64OneArg>::create($3);
          }
        | "(" "u32.const" integer ")" {
            $$ = Unary<NodeType::U32Const>::create($3);
          }
        | "(" "u64.const" integer ")" {
            $$ = Unary<NodeType::U64Const>::create($3);
          }
        | "(" "value" ")" {
            $$ = Nullary<NodeType::Value>::create();
          }
        | "(" "varint32" ")" {
            $$ = Nullary<NodeType::Varint32NoArgs>::create();
          }
        | "(" "varint32" integer ")" {
            $$ = Unary<NodeType::Varint32OneArg>::create($3);
          }
        | "(" "varint64" ")" {
            $$ = Nullary<NodeType::Varint64NoArgs>::create();
          }
        | "(" "varint64" integer ")" {
            $$ = Unary<NodeType::Varint64OneArg>::create($3);
          }
        | "(" "varuint1" ")" {
            $$ = Nullary<NodeType::Varuint1>::create();
          }
        | "(" "varuint7" ")" {
            $$ = Nullary<NodeType::Varuint7>::create();
          }
        | "(" "varuint32" ")" {
            $$ = Nullary<NodeType::Varuint32NoArgs>::create();
          }
        | "(" "varuint32" integer ")" {
            $$ = Unary<NodeType::Varuint32OneArg>::create($3);
          }
        | "(" "varuint64" ")" {
            $$ = Nullary<NodeType::Varuint64NoArgs>::create();
          }
        | "(" "varuint64" integer ")" {
            $$ = Unary<NodeType::Varuint64OneArg>::create($3);
          }
        | "(" "void" ")" {
            $$ = Nullary<NodeType::Void>::create();
          }
        | "(" "write" expression ")" {
            $$ = Unary<NodeType::Write>::create($3);
          }
        ;

extract_statement_list
        : statement { // first statement of extract
            $$ = Nary<NodeType::Extract>::create();
            $$->append($1);
          }
        | extract_statement_list statement { // remaining statements of extract.
            $$ = $1;
            $$->append($2);
          }
        ;

filter  : "(" "ast.to.ast" statement ")" {
            $$ = Unary<NodeType::AstToAst>::create($3);
          }
        | "(" "ast.to.bit" statement ")" {
            $$ = Unary<NodeType::AstToBit>::create($3);
          }
        | "(" "ast.to.byte" statement ")" {
            $$ = Unary<NodeType::AstToByte>::create($3);
          }
        | "(" "ast.to.int" statement ")" {
            $$ = Unary<NodeType::AstToInt>::create($3);
          }
        | "(" "bit.to.bit" statement ")" {
            $$ = Unary<NodeType::BitToBit>::create($3);
          }
        | "(" "bit.to.byte" statement ")" {
            $$ = Unary<NodeType::BitToByte>::create($3);
          }
        | "(" "bit.to.int" statement ")" {
            $$ = Unary<NodeType::BitToInt>::create($3);
          }
        | "(" "bit.to.ast" statement ")" {
            $$ = Unary<NodeType::BitToAst>::create($3);
          }
        | "(" "byte.to.bit" statement ")" {
            $$ = Unary<NodeType::ByteToBit>::create($3);
          }
        | "(" "byte.to.byte" statement ")" {
            $$ = Unary<NodeType::ByteToByte>::create($3);
          }
        | "(" "byte.to.int" statement ")" {
            $$ = Unary<NodeType::ByteToInt>::create($3);
          }
        | "(" "byte.to.ast" statement ")" {
            $$ = Unary<NodeType::ByteToAst>::create($3);
          }
        | "(" "int.to.bit" statement ")" {
            $$ = Unary<NodeType::IntToBit>::create($3);
          }
        | "(" "int.to.byte" statement ")" {
            $$ = Unary<NodeType::IntToByte>::create($3);
          }
        | "(" "int.to.int" statement ")" {
            $$ = Unary<NodeType::IntToInt>::create($3);
          }
        | "(" "int.to.ast" statement ")" {
            $$ = Unary<NodeType::IntToAst>::create($3);
          }
        ;

filter_list
        : filter {
            $$ = Nary<NodeType::Filter>::create();
            $$->append($1);
          }
        | filter_list filter {
            $$ = $1;
            $$->append($2);
          }
        ;

header  : "(" "version" integer ")" {
            $$ = Unary<NodeType::Version>::create($3);
          }
        ;

integer : INTEGER { $$ = IntegerNode::create($1); }
        ;


loop_body
        : "loop" expression {
            $$ = Nary<NodeType::Loop>::create();
            $$->append($2);
          }
        | "loop.unbounded" {
            $$ = Nary<NodeType::LoopUnbounded>::create();
          }
        | loop_body statement {
            $$ = $1;
            $$->append($2);
          }
        ;

method  : "(" "method" method_statement_list ")" { $$ = $3; }
        ;

method_list
        : "default" symbol statement {
          $$ = Nary<NodeType::Default>::create();
            $$->append($2);
            $$->append($3);
          }
        | "define" symbol statement {
            $$ = Nary<NodeType::Define>::create();
            $$->append($2);
            $$->append($3);
          }
        | method_list method { // Helper methods of define.
            $$ = $1;
            $$->append($2);
          }
        ;

method_statement_list
        : name statement { // Name and first statement of method
            $$ = Nary<NodeType::Method>::create();
            $$->append($1);
            $$->append($2);
          }
        | method_statement_list statement { // Remaining statements of method
            $$ = $1;
            $$->append($2);
          }
        ;

name    : IDENTIFIER { $$ = SymbolNode::create($1); }
        ;

section : "(" "section" declaration_list ")" { $$ = $3; }
        ;

section_list
        : header section { // Header / first section of file.
            $$ = Nary<NodeType::File>::create();
            $$->append($1);
            $$->append($2);
          }
        | section_list section { // Remaining sections in file.
            $$ = $1;
            $$->append($2);
          }
        ;

statement
        : expression { $$ = $1; }
        | "(" "copy" ")" { $$ = Nullary<NodeType::Copy>::create(); }
        | "(" "extract" extract_statement_list ")" { $$ = $3; }
        | "(" "filter" filter_list ")" { $$ = $3; }
        | "(" "if" expression statement statement ")" {
            $$ = IfThenElse::create($3, $4, $5);
          }
        | "(" loop_body ")" { $$ = $2; }
//        | "(" "loop" loop_statement_list ")" { $$ = $3; }
//        | "(" "loop.unbounded" loop_unbounded_statement_list ")" { $$ = $3; }
        | "(" "select" case_list ")" { $$ = $3; }
        | "(" "seq" statement_list ")" { $$ = $3; }
        ;

statement_list
        : %empty {
            $$ = Nary<NodeType::Sequence>::create();
          }
        | statement_list statement {
            $$ = $1;
            $$->append($2);
          }
        ;

symbol
        : QUOTED_IDENTIFIER { $$ = SymbolNode::create($1); }
        ;

%%

void wasm::filt::Parser::error(const location_type& L, const std::string& M) {
  Driver.error(L, M);
}

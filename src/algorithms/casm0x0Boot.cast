# Copyright 2016 WebAssembly Community Group participants
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Defines the boot CAST algorithm for reading/writing "Compressed binary
# algorithm" ("CASM") files.

(enclosing 'casm0x0-lits.cast')

(header (u32.const 0x6d736163) (u32.const 0x0))
(algorithm 1)

(define 'file'
  (locals 1)
  (block
    (eval 'symbol.table')
    (loop.unbounded
      (set (local 0) (uint8))
      (eval 'node' (local 0))
    )
  )
)

(define 'int.value' (params 1)
  (=> 'int.value.begin')
  (if (uint8)                   # 0 or (decode::ValueFormat+1)
    (param 0)                   # integer value.
  )
  (=> 'int.value.end')
)

(define 'nary.node'
  (varuint32)
  (=> 'nary.inst')
)

(define 'node' (params 1)
  (switch (param 0)
    (error)
    (case 'algorithm'
     case 'bit'
     case 'block'
     case 'case'
     case 'error'
     case 'if.then'
     case 'literal.action.use'
     case 'literal.define'
     case 'literal.use'
     case 'loop'
     case 'loop.unbounded'
     case 'no.locals'
     case 'no.params'
     case 'one'
     case 'rename'
     case 'uint8'
     case 'varint32'
     case 'varint64'
     case 'varuint32'
     case 'varuint64'
     case 'void'
     case 'zero'
     case '=>'                  (=> 'postorder.inst'))

    (case 'define'
     case 'eval'
     case 'file'
     case 'literal.action.enum'
     #case 'section'
     case 'sequence'
     case 'switch'              (eval 'nary.node'))

    (case 'param'
     case 'params'
     case 'u32.const'
                                (eval 'int.value' (varuint32)))
    (case 'u8.const'            (eval 'int.value' (uint8)))
    (case 'u64.const'           (eval 'int.value' (varuint64)))
    (case 'symbol'              (eval 'symbol.lookup'))
  )
)

(define 'symbol.lookup'
  (varuint32)
  (=> 'symbol.lookup')
)

(define 'symbol.name'
  (loop
    (seq
      (varuint32)
      (=> 'symbol.name.begin'))
    (uint8)
  )
  (=> 'symbol.name.end')
)

(define 'symbol.table'
  (loop
    (varuint32)
    (eval 'symbol.name')
  )
)

# Copyright 2016 WebAssembly Community Group participants
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Defines the CAST algorithm for reading/writing WASM files.

(enclosing 'wasm0xd-lits.cast')

(header (u32.const 0x6d736163) (u32.const 0x0))
(header.read (u32.const 0x6d736100) (u32.const 0xd))

(declarations)

(define 'br_table' (params)
  (=> 'br_table.begin')
  (loop
    (seq (varuint32) (=> 'br_table.count'))
    (varuint32)
    (=> 'br_table.target'))
  (varuint32)
  (=> 'br_table.default')
  (=> 'br_table.end')
)

(define 'br_target' (params)
   (=> 'br_target.begin')
   (varuint32)
   (=> 'br_target.end')
)

(define 'code.section' (params)
  (=> 'code.section.begin')
  (loop
    (seq (varuint32) (=> 'code.section.count'))
    (eval 'function.body'))
  (=> 'code.section.end')
)

(define 'data.section' (params)
  (=> 'data.section.begin')
  (loop
    (seq (varuint32) (=> 'data.section.count'))
    (eval 'data.segment'))
  (=> 'data.section.end')
)

(define 'data.segment' (params)
  (=> 'data.segment.begin')
  (varuint32)
  (=> 'data.segment.memory_index')
  (eval 'init_expr')        # an i32 initializer defining offset to place data.
  (loop 
    (seq (varuint32) (=> 'data.segment.size'))
    (uint8)
    (=> 'data.segment.data'))
  (=> 'data.segment.end')
)

(define 'element.section' (params)
  (=> 'element.section.begin')
  (loop
    (seq (varuint32) (=> 'element.section.count'))
    (eval 'element.segment')
  )
  (=> 'element.section.end')
)

(define 'element.segment' (params)
  (=> 'element.segment.begin')
  (varuint32)                 # The table index
  (=> 'element.segment.index')
  (eval 'init_expr')         # An i32 initializer that computess the
                             # placement offset.
  (=> 'element.segment.offset')
  (loop
    (seq (varuint32) (=> 'element.segment.num_elem'))
    (varuint32)
    (=> 'element.segment.elem')
  )
  (=> 'element.segment.end')
)

(define 'export.section' (params)
  (=> 'export.section.begin')
  (loop 
    (seq (varuint32) (=> 'export.section.count'))
    (eval 'export.entry'))
  (=> 'export.section.end')
)

(define 'export.entry' (params)
  (eval 'symbol.name')       # Exported field name.
  (=> 'export.entry.name')
  (eval 'external.kind')
  (varuint32)
  (=> 'export.entry.index')
)

(define 'external.kind' (params)
  (=> 'external.kind.begin')
  (uint8)
  (=> 'external.kind.end')
)

(define 'file' (params)
  (=> 'file.begin')
  (loop.unbounded
    (eval 'section')
  )
  (=> 'file.end')
)

(define 'function.section' (params)
  (=> 'function.section.begin')
  (loop
    (seq (varuint32) (=> 'function.section.count'))
    (varuint32)
    (=> 'function.section.type')
  )
  (=> 'function.section.end')
)

(define 'function.names' (params)
  (=> 'function.names.begin')
  (eval 'symbol.name')       # function name
  (=> 'function.names.fcn_name')
  (loop
    (seq (varuint32) (=> 'function.names.local_count'))
    (eval 'symbol.name')
    (=> 'function.names.local_name')
  )
  (=> 'function.names.end')
)

(define 'function.type' (params)
  (=> 'function.type.begin')
  (varint32)  # type form
  (=> 'function.type.form')
  (loop
    (seq (varuint32) (=> 'type.num.params'))
    (eval 'type.value')
    (=> 'type.param')
  )
  (loop
    (seq (varuint32) (=> 'type.num.returns'))
    (eval 'type.value')
    (=> 'type.return')
  )
  (=> 'function.type.end')
)

(define 'function.body' (params)
  (=> 'function.body.begin')
  (block
    (loop
      (seq (varuint32) (=> 'function.body.local_count'))
      (eval 'local.entry'))
    (=> 'function.body.code.begin')
    (loop.unbounded
      (eval 'instruction.opcode')     # reads instruction opcode
      (eval 'instruction'))      # selects/parses corresponding instruction.
    (=> 'function.body.code.end')
  )
  (=> 'function.body.end')
)

(define 'global.section' (params)
  (=> 'global.section.begin')
  (loop
    (seq (varuint32) (=> 'global.section.count'))
    (eval 'global.type')
    (=> 'global.section.variable.type')
    (eval 'init_expr')
    (=> 'global.section.variable.init')
  )
  (=> 'global.section.end')
)

(define 'global.type' (params)
  (=> 'global.type.begin')
  (eval 'type.value')
  (uint8)
  (=> 'global.type.end')
)

(define 'import.section' (params)
  (=> 'import.section.begin')
  (loop 
    (seq (varuint32) (=> 'import.section.count'))
    (eval 'import.entry')
  )
  (=> 'import.section.end')
)

(define 'import.entry' (params)
  (=> 'import.entry.begin')
  (eval 'symbol.name')
  (=> 'import.entry.module')
  (eval 'symbol.name')
  (=> 'import.entry.field')
  (switch (eval 'external.kind')
    (error)
    (case 'external.kind.function'
      (varuint32)
      (=> 'import.entry.function'))
    (case 'external.kind.table'
      (eval 'table.type')
      (=> 'import.entry.table'))
    (case 'external.kind.memory'
      (eval 'memory.type')
      (=> 'import.entry.memory'))
    (case 'external.kind.global'
      (eval 'global.type')
      (=> 'import.entry.global'))
  )
  (=> 'import.entry.end')
)

(define 'init_expr' (params)
  (locals 1)
  (=> 'init_expr.begin')
  (set (local 0) (u32.const 1))
  (eval 'instruction.opcode')      # Opcode of expression.
  (switch (seq (read) (=> 'init_expr.opcode'))
    (error)                 # Only allows explicitly allowed operands.
    (case 'inst.i32.const'
      (varint32)
      (=> 'inst.i32.const'))
    (case 'inst.i64.const'
      (varint64)
      (=> 'inst.i64.const'))
    (case 'inst.f32.const'
      (uint32)
      (=> 'inst.f32.const'))
    (case 'inst.f64.const'
      (uint64)
      (=> 'inst.f64.const'))
    (case 'inst.get_global'
      (varuint32)
      (=> 'inst.get_global'))
    (case 'inst.end'
      (=> 'inst.end')
      (set (local 0) (u32.const 0)))
  )
  (if (local 0)
    (switch (eval 'instruction.opcode')
      (error)
      (case 'inst.end' (=> 'inst.end'))
    )
  )
  (=> 'init_expr.end')
)

(define 'instruction' (params)
  (=> 'instruction.begin')
  (switch (seq (read) (=> 'instruction.opcode'))
    (error)
    # control flow operators.
    (case 'inst.unreachable' (=> 'inst.unreachable'))
    (case 'inst.nop' (=> 'inst.nop'))
    (case 'inst.block'
      (eval 'type.value')
      (=> 'inst.block'))
    (case 'inst.loop'
      (eval 'type.value')
      (=> 'inst.loop'))
    (case 'inst.if'
      (eval 'type.value')
      (=> 'inst.if'))
    (case 'inst.else' (=> 'inst.else'))
    (case 'inst.end' (=> 'inst.end'))
    (case 'inst.br'
      (eval 'br_target')
      (=> 'inst.br'))
    (case 'inst.br_if'
      (eval 'br_target')
      (=> 'inst.br_if'))
    (case 'inst.br_table'
      (eval 'br_table')
      (=> 'inst.br_table'))
    (case 'inst.return'
      (=> 'inst.return'))

    # Call operators.
    (case 'inst.call'
      (varuint32) # function index
      (=> 'inst.call'))
    (case 'inst.call_indirect'
      (varuint32) # function index
      (varuint32) # reserved
      (=> 'inst.call_indirect'))

    # Parametric operators
    (case 'inst.drop'
      (=> 'inst.drop'))
    (case 'inst.select'
      (=> 'inst.select'))

    # Variable access
    (case 'inst.get_local'
      (varuint32)
      (=> 'inst.get_local'))
    (case 'inst.set_local'
      (varuint32)
      (=> 'inst.set_local'))
    (case 'inst.tee_local'
      (varuint32)
      (=> 'inst.tee_local'))
    (case 'inst.get_global'
      (varuint32)
      (=> 'inst.get_global'))
    (case 'inst.set_global'
      (varuint32)
      (=> 'inst.set_global'))

    # Memory-related operators
    (case 'inst.i32.load'
      (eval 'memory.immediate')
      (=> 'inst.i32.load'))
    (case 'inst.i64.load'
      (eval 'memory.immediate')
      (=> 'inst.i64.load'))
    (case 'inst.f32.load'
      (eval 'memory.immediate')
      (=> 'inst.f32.load'))
    (case 'inst.f64.load'
      (eval 'memory.immediate')
      (=> 'inst.f64.load'))
    (case 'inst.i32.load8_s'
      (eval 'memory.immediate')
      (=> 'inst.i32.load8_s'))
    (case 'inst.i32.load8_u'
      (eval 'memory.immediate')
      (=> 'inst.i32.load8_u'))
    (case 'inst.i32.load16_s'
      (eval 'memory.immediate')
      (=> 'inst.i32.load16_s'))
    (case 'inst.i32.load16_u'
      (eval 'memory.immediate')
      (=> 'inst.i32.load16_u'))
    (case 'inst.i64.load8_s'
      (eval 'memory.immediate')
      (=> 'inst.i64.load8_s'))
    (case 'inst.i64.load8_u'
      (eval 'memory.immediate')
      (=> 'inst.i64.load8_u'))
    (case 'inst.i64.load16_s'
      (eval 'memory.immediate')
      (=> 'inst.i64.load16_s'))
    (case 'inst.i64.load16_u'
      (eval 'memory.immediate')
      (=> 'inst.i64.load16_u'))
    (case 'inst.i64.load32_s'
      (eval 'memory.immediate')
      (=> 'inst.i64.load32_s'))
    (case 'inst.i64.load32_u'
      (eval 'memory.immediate')
      (=> 'inst.i64.load32_u'))
    (case 'inst.i32.store'
      (eval 'memory.immediate')
      (=> 'inst.i32.store'))
    (case 'inst.i64.store'
      (eval 'memory.immediate')
      (=> 'inst.i64.store'))
    (case 'inst.f32.store'
      (eval 'memory.immediate')
      (=> 'inst.f32.store'))
    (case 'inst.f64.store'
      (eval 'memory.immediate')
      (=> 'inst.f64.store'))
    (case 'inst.i32.store8'
      (eval 'memory.immediate')
      (=> 'inst.i32.store8'))
    (case 'inst.i32.store16'
      (eval 'memory.immediate')
      (=> 'inst.i32.store16'))
    (case 'inst.i64.store8'
      (eval 'memory.immediate')
      (=> 'inst.i64.store8'))
    (case 'inst.i64.store16'
      (eval 'memory.immediate')
      (=> 'inst.i64.store16'))
    (case 'inst.i64.store32'
      (eval 'memory.immediate')
      (=> 'inst.i64.store32'))
    (case 'inst.current_memory'
      (varuint32)
      (=> 'inst.current_memory'))
    (case 'inst.grow_memory'
      (varuint32)
      (=> 'inst.grow_memory'))

    # Constants
    (case 'inst.i32.const'
      (varint32)
      (=> 'inst.i32.const'))
    (case 'inst.i64.const'
      (varint64)
      (=> 'inst.i64.const'))
    (case 'inst.f32.const'
      (uint32)
      (=> 'inst.f32.const'))
    (case 'inst.f64.const'
      (uint64)
      (=> 'inst.f64.const'))

    # Comparison operators
    (case 'inst.i32.eqz'  (=> 'inst.i32.eqz'))
    (case 'inst.i32.eq'   (=> 'inst.i32.eq'))
    (case 'inst.i32.ne'   (=> 'inst.i32.ne'))
    (case 'inst.i32.lt_s' (=> 'inst.i32.lt_s'))
    (case 'inst.i32.lt_u' (=> 'inst.i32.lt_u'))
    (case 'inst.i32.gt_s' (=> 'inst.i32.gt_s'))
    (case 'inst.i32.gt_u' (=> 'inst.i32.gt_u'))
    (case 'inst.i32.le_s' (=> 'inst.i32.le_s'))
    (case 'inst.i32.le_u' (=> 'inst.i32.le_u'))
    (case 'inst.i32.ge_s' (=> 'inst.i32.ge_s'))
    (case 'inst.i32.ge_u' (=> 'inst.i32.ge_u'))
    (case 'inst.i64.eqz'  (=> 'inst.i64.eqz'))
    (case 'inst.i64.eq'   (=> 'inst.i64.eq'))
    (case 'inst.i64.ne'   (=> 'inst.i64.ne'))
    (case 'inst.i64.lt_s' (=> 'inst.i64.lt_s'))
    (case 'inst.i64.lt_u' (=> 'inst.i64.lt_u'))
    (case 'inst.i64.gt_s' (=> 'inst.i64.gt_s'))
    (case 'inst.i64.gt_u' (=> 'inst.i64.gt_u'))
    (case 'inst.i64.le_s' (=> 'inst.i64.le_s'))
    (case 'inst.i64.le_u' (=> 'inst.i64.le_u'))
    (case 'inst.i64.ge_s' (=> 'inst.i64.ge_s'))
    (case 'inst.i64.ge_u' (=> 'inst.i64.ge_u'))
    (case 'inst.f32.eq'   (=> 'inst.f32.eq'))
    (case 'inst.f32.ne'   (=> 'inst.f32.ne'))
    (case 'inst.f32.lt'   (=> 'inst.f32.lt'))
    (case 'inst.f32.gt'   (=> 'inst.f32.gt'))
    (case 'inst.f32.le'   (=> 'inst.f32.le'))
    (case 'inst.f32.ge'   (=> 'inst.f32.ge'))
    (case 'inst.f64.eq'   (=> 'inst.f64.eq'))
    (case 'inst.f64.ne'   (=> 'inst.f64.ne'))
    (case 'inst.f64.lt'   (=> 'inst.f64.lt'))
    (case 'inst.f64.gt'   (=> 'inst.f64.gt'))
    (case 'inst.f64.le'   (=> 'inst.f64.le'))
    (case 'inst.f64.ge'   (=> 'inst.f64.ge'))

    # Numeric operators
    (case 'inst.i32.clz'      (=> 'inst.i32.clz'))
    (case 'inst.i32.ctx'      (=> 'inst.i32.ctx'))
    (case 'inst.i32.popcnt'   (=> 'inst.i32.popcnt'))
    (case 'inst.i32.add'      (=> 'inst.i32.add'))
    (case 'inst.i32.sub'      (=> 'inst.i32.sub'))
    (case 'inst.i32.mul'      (=> 'inst.i32.mul'))
    (case 'inst.i32.div_s'    (=> 'inst.i32.div_s'))
    (case 'inst.i32.div_u'    (=> 'inst.i32.div_u'))
    (case 'inst.i32.rem_s'    (=> 'inst.i32.rem_s'))
    (case 'inst.i32.rem_u'    (=> 'inst.i32.rem_u'))
    (case 'inst.i32.and'      (=> 'inst.i32.and'))
    (case 'inst.i32.or'       (=> 'inst.i32.or'))
    (case 'inst.i32.xor'      (=> 'inst.i32.xor'))
    (case 'inst.i32.shl'      (=> 'inst.i32.shl'))
    (case 'inst.i32.shr_s'    (=> 'inst.i32.shr_s'))
    (case 'inst.i32.shr_u'    (=> 'inst.i32.shr_u'))
    (case 'inst.i32.rotl'     (=> 'inst.i32.rotl'))
    (case 'inst.i32.rotr'     (=> 'inst.i32.rotr'))
    (case 'inst.i64.clz'      (=> 'inst.i64.clz'))
    (case 'inst.i64.ctx'      (=> 'inst.i64.ctx'))
    (case 'inst.i64.popcnt'   (=> 'inst.i64.popcnt'))
    (case 'inst.i64.add'      (=> 'inst.i64.add'))
    (case 'inst.i64.sub'      (=> 'inst.i64.sub'))
    (case 'inst.i64.mul'      (=> 'inst.i64.mul'))
    (case 'inst.i64.div_s'    (=> 'inst.i64.div_s'))
    (case 'inst.i64.div_u'    (=> 'inst.i64.div_u'))
    (case 'inst.i64.rem_s'    (=> 'inst.i64.rem_s'))
    (case 'inst.i64.rem_u'    (=> 'inst.i64.rem_u'))
    (case 'inst.i64.and'      (=> 'inst.i64.and'))
    (case 'inst.i64.or'       (=> 'inst.i64.or'))
    (case 'inst.i64.xor'      (=> 'inst.i64.xor'))
    (case 'inst.i64.shl'      (=> 'inst.i64.shl'))
    (case 'inst.i64.shr_s'    (=> 'inst.i64.shr_s'))
    (case 'inst.i64.shr_u'    (=> 'inst.i64.shr_u'))
    (case 'inst.i64.rotl'     (=> 'inst.i64.rotl'))
    (case 'inst.i64.rotr'     (=> 'inst.i64.rotr'))
    (case 'inst.f32.abs'      (=> 'inst.f32.abs'))
    (case 'inst.f32.neg'      (=> 'inst.f32.neg'))
    (case 'inst.f32.ceil'     (=> 'inst.f32.ceil'))
    (case 'inst.f32.floor'    (=> 'inst.f32.floor'))
    (case 'inst.f32.trunc'    (=> 'inst.f32.trunc'))
    (case 'inst.f32.nearest'  (=> 'inst.f32.nearest'))
    (case 'inst.f32.sqrt'     (=> 'inst.f32.sqrt'))
    (case 'inst.f32.add'      (=> 'inst.f32.add'))
    (case 'inst.f32.sub'      (=> 'inst.f32.sub'))
    (case 'inst.f32.mul'      (=> 'inst.f32.mul'))
    (case 'inst.f32.div'      (=> 'inst.f32.div'))
    (case 'inst.f32.min'      (=> 'inst.f32.min'))
    (case 'inst.f32.max'      (=> 'inst.f32.max'))
    (case 'inst.f32.copysign' (=> 'inst.f32.copysign'))
    (case 'inst.f64.abs'      (=> 'inst.f64.abs'))
    (case 'inst.f64.neg'      (=> 'inst.f64.neg'))
    (case 'inst.f64.ceil'     (=> 'inst.f64.ceil'))
    (case 'inst.f64.floor'    (=> 'inst.f64.floor'))
    (case 'inst.f64.trunc'    (=> 'inst.f64.trunc'))
    (case 'inst.f64.nearest'  (=> 'inst.f64.nearest'))
    (case 'inst.f64.sqrt'     (=> 'inst.f64.sqrt'))
    (case 'inst.f64.add'      (=> 'inst.f64.add'))
    (case 'inst.f64.sub'      (=> 'inst.f64.sub'))
    (case 'inst.f64.mul'      (=> 'inst.f64.mul'))
    (case 'inst.f64.div'      (=> 'inst.f64.div'))
    (case 'inst.f64.min'      (=> 'inst.f64.min'))
    (case 'inst.f64.max'      (=> 'inst.f64.max'))
    (case 'inst.f64.copysign' (=> 'inst.f64.copysign'))

    # Conversions
    (case 'inst.i32.wrap/i64'      (=> 'inst.i32.wrap/i64'))
    (case 'inst.i32.trunc_s/f32'   (=> 'inst.i32.trunc_s/f32'))
    (case 'inst.i32.trunc_u/f32'   (=> 'inst.i32.trunc_u/f32'))
    (case 'inst.i32.trunc_s/f64'   (=> 'inst.i32.trunc_s/f64'))
    (case 'inst.i32.trunc_u/f64'   (=> 'inst.i32.trunc_u/f64'))
    (case 'inst.i64.extend_s/i32'  (=> 'inst.i64.extend_s/i32'))
    (case 'inst.i64.extend_u/i32'  (=> 'inst.i64.extend_u/i32'))
    (case 'inst.i64.trunc_s/f32'   (=> 'inst.i64.trunc_s/f32'))
    (case 'inst.i64.trunc_u/f32'   (=> 'inst.i64.trunc_u/f32'))
    (case 'inst.i64.trunc_s/f64'   (=> 'inst.i64.trunc_s/f64'))
    (case 'inst.i64.trunc_u/f64'   (=> 'inst.i64.trunc_u/f64'))
    (case 'inst.f32.convert_s/i32' (=> 'inst.f32.convert_s/i32'))
    (case 'inst.f32.convert_u/i32' (=> 'inst.f32.convert_u/i32'))
    (case 'inst.f32.convert_s/i64' (=> 'inst.f32.convert_s/i64'))
    (case 'inst.f32.convert_u/i64' (=> 'inst.f32.convert_u/i64'))
    (case 'inst.f32.demote/f64'    (=> 'inst.f32.demote/f64'))
    (case 'inst.f64.convert_s/i32' (=> 'inst.f64.convert_s/i32'))
    (case 'inst.f64.convert_u/i32' (=> 'inst.f64.convert_u/i32'))
    (case 'inst.f64.convert_s/i64' (=> 'inst.f64.convert_s/i64'))
    (case 'inst.f64.convert_u/i64' (=> 'inst.f64.convert_u/i64'))
    (case 'inst.f64.promote/f32'   (=> 'inst.f64.promote/f32'))

    # Reinterpretations
    (case 'inst.i32.reinterpret/f32' (=> 'inst.i32.reinterpret/f32'))
    (case 'inst.i64.reinterpret/f64' (=> 'inst.i64.reinterpret/f64'))
    (case 'inst.f32.reinterpret/i32' (=> 'inst.f32.reinterpret/i32'))
    (case 'inst.f64.reinterpret/i64' (=> 'inst.f64.reinterpret/i64'))
  )
  (=> 'instruction.end')
)

(define 'instruction.opcode' (params) (uint8))

(define 'local.entry' (params)
  (=> 'local.entry.begin')
  (varuint32)
  (=> 'local.entry.count')
  (eval 'type.value')
  (=> 'local.entry.end')
)

(define 'memory.section' (params)
  (=> 'memory.section.begin')
  (loop
    (seq (varuint32) (=> 'memory.section.count'))
    (eval 'memory.type')
    (=> 'memory.section.entry')
  )
  (=> 'memory.section.end')
)

(define 'memory.immediate' (params)
  (=> 'memory.immediate.begin')
  (varuint32)                  # log2 alignment/flags
  (varuint32)                  # offset
  (=> 'memory.immediate.end')
)

(define 'name.section' (params) # todo: fix.
  (=> 'name.section.begin')
  (loop
    (seq (varuint32) (=> 'name.section.count'))
    (eval 'function.names'))
  (=> 'name.section.end')
)

(define 'memory.type' (params)
  (=> 'memory.type.begin')
  (eval 'resizable.limits')
  (=> 'memory.type.end')
)

(define 'resizable.limits' (params)
  (locals 1)
  (=> 'resizable.limits.begin')
  (set (local 0) (varuint32))
  (=> 'resizable.limits.flags')
  (varuint32)
  (=> 'resizable.limits.initial')
  (if (bitwise.and (local 0) (u32.const 0x1))
    (seq
      (varuint32)
      (=> 'resizable.limits.maximum')
    )
  )
  (=> 'resizable.limits.end')
)

(define 'section' (params)
  (locals 1)
  (=> 'section.begin')
  (set (local 0) (varuint32))
  (=> 'section.code')
  (block
    (switch (local 0)
      (error)
      (case 'unknown.section' (eval 'unknown.section'))
      (case 'type.section' (eval 'type.section'))
      (case 'import.section' (eval 'import.section'))
      (case 'function.section' (eval 'function.section'))
      (case 'table.section' (eval 'table.section'))
      (case 'memory.section' (eval 'memory.section'))
      (case 'global.section' (eval 'global.section'))
      (case 'export.section' (eval 'export.section'))
      (case 'start.section' (eval 'start.section'))
      (case 'element.section' (eval 'element.section'))
      (case 'code.section' (eval 'code.section'))
      (case 'data.section' (eval 'data.section'))
    )
  )
  (=> 'section.end')
)

(define 'skip.section' (params)
  (=> 'skip.section.begin')
  (loop.unbounded (uint8))
  (=> 'skip.section.end')
)

(define 'start.section' (params)
  (=> 'start.section.begin')
  (varuint32)
  (=> 'start.section.end')
)

(define 'symbol.name' (params)
  # Note: This define is understood by the reader. (TODO: karlschimpf)
  (=> 'symbol.name.begin')
  (loop
    (seq (varuint32) (=> 'symbol.name.length'))
    (uint8))
  (=> 'symbol.name.end')
)

(define 'table.section' (params)
  (=> 'table.section.begin')
  (loop
    (seq (varuint32) (=> 'table.section.count'))
    (eval 'table.type')
    (=> 'table.section.type')
  )
  (=> 'table.section.end')
) 

(define 'table.type' (params)
  (=> 'table.type.begin')
  (eval 'type.value')
  (eval 'resizable.limits')
  (=> 'table.type.end')
)

(define 'type.section' (params)
  (=> 'type.section.begin')
  (loop (seq (varuint32) (=> 'type.section.count'))
    (eval 'function.type')
  )
  (=> 'type.section.end')
)

(define 'type.value' (params)
  (=> 'type.value.begin')
  (varint32)
  (=> 'type.value.end')
)

(define 'unknown.section' (params)
  (=> 'unknown.section.begin')
  (eval 'symbol.name')
  (eval 'unknown_body')
  (=> 'unknown.section.end')
)

(define 'unknown_body' (params)
  (if (last.symbol.is 'name')
    (eval 'name.section')
    (eval 'skip.section')
  )
)
